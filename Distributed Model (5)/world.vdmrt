class World

instance variables
    io : IO := new IO();
    current_time : real := 0;

values
    target_angle : real = 55;
    accuracy : real = 0.001; 
    lower_bound : real = target_angle * (1 - accuracy);
    upper_bound : real = target_angle * (1 + accuracy);

operations
    public UpdateTimeStamp : () ==> ()
    UpdateTimeStamp() == duration(0) (
        current_time := time / 1E9;
        echoState();
    );

    public echoHeader: () ==> ()
    echoHeader() == (
        dcl str : seq of char := "Current Time - Angle - Torque - Motor State\n";
        def - = io.echo(str) in skip;
    );

    public round: real ==> real
    round(r) == (
        dcl scaled: real := r * 1000;
        dcl rounded: real := floor(scaled + 0.5);
        return rounded / 1000;
    );

    public echoState: () ==> ()
    echoState() == (
        dcl str : seq of char := "";
        str := str ^ VDMUtil`val2seq_of_char[real](round(current_time));
        str := str ^ " - ";
        str := str ^ VDMUtil`val2seq_of_char[real](round(MySystem`robot_arm.env_angle));
        str := str ^ " - ";
        str := str ^ VDMUtil`val2seq_of_char[real](round(MySystem`motor.GetTorque()));
        str := str ^ " - ";
        str := str ^ VDMUtil`val2seq_of_char[<On> | <Off>](MySystem`motor.GetMotorState());
        str := str ^ "\n";
        def - = io.echo(str) in skip;
    );

    public Scenario0 : () ==> bool  --PID Test Scenario
    Scenario0() == (
        echoHeader();
        UpdateTimeStamp();
        MySystem`controller.Set_Motor_State(<On>);
        MySystem`controller.Set_Target_Angle(35);

        while (abs(MySystem`controller.Get_Last_Error()) > 0.2) do (
            UpdateTimeStamp();
            MySystem`controller.MonitorCurrentAngle();
            MySystem`controller.MonitorCurrentAngVel();
            MySystem`controller.MoveTowardsTargetAngle();
            MySystem`motor.Step();
            MySystem`joint.Step();
        );
        return true;
    );

    public Scenario1 : () ==> bool  -- Quick Change Response Test
    Scenario1() == (
        dcl reachedFirstTarget : bool := false;
        echoHeader();
        UpdateTimeStamp();
        MySystem`controller.Set_Motor_State(<On>);

        -- Initial target angle
        MySystem`controller.Set_Target_Angle(35);

        while (true) do (
            UpdateTimeStamp();

            -- Check if first target is reached
            if not reachedFirstTarget and abs(MySystem`controller.Get_Last_Error()) < 0.2 then (
                reachedFirstTarget := true;
                -- Change to a new target angle suddenly
                MySystem`controller.Set_Target_Angle(-35); -- Opposite direction to test responsiveness
            );

            MySystem`controller.MonitorCurrentAngle();
            MySystem`controller.MonitorCurrentAngVel();
            MySystem`controller.MoveTowardsTargetAngle();
            MySystem`motor.Step();
            MySystem`joint.Step();

            -- Break loop if second target is reached
            if reachedFirstTarget and abs(MySystem`controller.Get_Last_Error()) < 0.2 then (
                return true;
            );
        );
    );

end World